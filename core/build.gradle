plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.dargoz.pdfium.core'
    compileSdk 35
    ndkVersion "28.2.13676358"

    defaultConfig {
        minSdk 21
        targetSdk 35

        aarMetadata {
            minCompileSdk = 21
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.dargoz.pdfium'
            artifactId = 'pdfium-android'
            version = '2.1.0'

            afterEvaluate {
                from components.release
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}